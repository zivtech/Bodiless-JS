// The following credentials must be set up in Jenkins, 
// at instance or at folder level:
// - npm-registry-url (string, ex: '//registry.npmjs.org/')
// - git-host (string)
// - jenkins-service-account ('Username with password')
// - npm-auth-token (string)
// - git-url (string)


pipeline {
    agent { label 'node' }
    options {
        disableConcurrentBuilds()
        timeout(activity: true, time: 1, unit: 'HOURS')
        timestamps()
    }
    triggers {
        pollSCM ''
    }
    stages {
        stage('install') {
            steps {
                script {
                    postBuildStatus(defaultBuildState(), env.GIT_COMMIT)
                    sh 'npm ci'
                    sh 'npm run bootstrap'
                }
            }
        }
        stage('build') {
            steps {
                sh 'npm run build'
            }
        }
        stage('lint') {
            when {
                expression { isCI() }
            }
            steps {
                sh 'npm run lint'
            }
        }
        stage('test') {
            when {
                expression { isCI() }
            }
            steps {
                sh 'npm run test'
            }
        }
        stage('publish') {
            steps {
                script {
                    withCredentials([
                        string(credentialsId: 'npm-registry-url', variable: 'NPM_REGISTRY_URL'),
                        string(credentialsId: 'npm-auth-token', variable: 'NPM_AUTH_TOKEN')
                    ]) {
                        def NPM_REGISTRY_URL_WITH_PROTOCOL = withHttpsProtocol(NPM_REGISTRY_URL)
                        sh "echo $NPM_REGISTRY_URL:_authToken=$NPM_AUTH_TOKEN > ~/.npmrc"
                        if (isCI()) {
                            sh "npm run lerna -- publish --canary --yes --registry $NPM_REGISTRY_URL_WITH_PROTOCOL"
                        } else {
                            sh "npm run lerna -- publish from-package --yes --registry $NPM_REGISTRY_URL_WITH_PROTOCOL"
                        }
                    }
                }
            }
        }
    }
    post {
        success {
            postBuildStatus(defaultBuildState("SUCCESSFUL"), env.GIT_COMMIT)
        }
        failure {
            postBuildStatus(defaultBuildState("FAILED"), env.GIT_COMMIT)
        }
        aborted {
            postBuildStatus(defaultBuildState("FAILED"), env.GIT_COMMIT)
        }
    }
}

Boolean isCI() {
    //def isTag = sh(returnStdout: true, script: "git tag --contains ${scmVars.GIT_COMMIT}")
    //return env.GIT_BRANCH == "master" && !isTag
    return false
}
String withHttpsProtocol(String url) {
    return "https:" + url
}

class BuildState {
    String state //INPROGRESS|SUCCESSFUL|FAILED
    String key
    String name
    String url
    String description
    String toJson() {
        return groovy.json.JsonOutput.toJson(this)
    }
}
BuildState defaultBuildState(String state = 'INPROGRESS') {
    BuildState bs = new BuildState()
    bs.state = state
    bs.key = "jenkins"
    bs.name = "publish packages"
    bs.url = "${env.BUILD_URL}"
    bs.description = "publish bodiless packages"
    return bs
}
void postBuildStatus(BuildState bs, String commit) {
    withCredentials([
        string(credentialsId: 'git-host', variable: 'GIT_HOST'),
        usernamePassword(credentialsId: 'jenkins-service-account', usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')
    ]) {
        String script = "curl -u $USERNAME:$PASSWORD -X POST -H \"Content-Type: application/json\" https://${GIT_HOST}/rest/build-status/1.0/commits/${commit} -d '${bs.toJson()}'"
        String result = sh(script: script, returnStdout: true).trim()
        echo result
    }
}